--- MAUDE_HCS: maude_hcs
---
--- Software Markings (UNCLASS)
--- PWNDD Software
---
--- Copyright (C) 2025 RTX BBN Technologies Inc. All Rights Reserved
---
--- Contract No: HR00112590083
--- Contractor Name: RTX BBN Technologies Inc.
--- Contractor Address: 10 Moulton Street, Cambridge, Massachusetts 02138
---
--- The U.S. Government's rights to use, modify, reproduce, release, perform,
--- display, or disclose these technical data and software are defined in the
--- Article VII: Data Rights clause of the OTA.
---
--- This document does not contain technology or technical data controlled under
--- either the U.S. International Traffic in Arms Regulations or the U.S. Export
--- Administration Regulations.
---
--- DISTRIBUTION STATEMENT A: Approved for public release; distribution is
--- unlimited.
---
--- Notice: Markings. Any reproduction of this computer software, computer
--- software documentation, or portions thereof must also reproduce the markings
--- contained herein.
---
--- MAUDE_HCS: end

*** Load DNS aux for the types, even though this is not a DNS related actor.
load ../../../../deps/dns_formalization/Maude/src/common/_aux

mod MASTODON_NODE is
  pr STRING .
  pr AUX-COMMON .

  --- A Mastodon Toot Status or Media ID.
  sorts MasId .
  subsort Nat < MasId .
  --- A Mastodon list of MasIds.
  sorts MasIdList .
  subsort MasId < MasIdList .
  --- A media / image file name.
  sorts MediaFilename .
  subsort String < MediaFilename .
  --- A list of media file names.
  sorts MediaFilenameList .
  subsort MediaFilename < MediaFilenameList .
  --- A client Program Counter operation.
  sorts Op .
  subsort String < Op .
  --- A client Program Counter.
  sorts OpStack .
  subsort Op < OpStack .
  --- A Mastodon post / status.
  sorts Url .
  subsort String < Url .
  sorts UrlList .
  subsort Url < UrlList .

  ---- Used by iodine server when extracting information from Query
  sorts AttributeSetMsgs .
  ----              serverAttrs Msgs

  --- TODO: Merge with the same definition in Iodine (should probably be moved to a common .maude file)
  op nullMsg : -> Msg .
  op `{_`,_`} : AttributeSet Config -> AttributeSetMsgs [ctor] .

  --- ----------
  --- [Messages]
  --- ----------
  --- An HTTP request has:
  --- A direction: GET, POST
  ---   GETs have a type (url, statuses, media, search, timelines, etc.)
  ---     Each type has different options
  ---       Getting a file: url
  ---       statuses: string of toot message
  ---       timelines: hashtag, limit (optional)

  ---   POSTs have a type (statuses, media, etc.)
  ---     statuses: have a text, may have attachment Ids
  ---     media: have an image


  --- An HTTP response has:
  ---   A status (OK, ACCEPTED, etc.)
  ---   A json-formatted response of posts
  ---     For POST request, post will include new media or status Id
  ---     For GET request, post will include a list of posts with attachment urls
  sorts HttpMsg HttpRequest HttpResponse .
  sorts GetRequest PostRequest .
  --- A Toot is a post in Mastodon lexicon.  To differenciate between the object, rendered on a client,
  --- we created ResponseToot, which is the Toot as it appears in an HTTP Response---maybe it is the 
  --- under the cover.
  sorts ResponseToot .
  sorts ResponseTootList .

  --- Tell Maude this is compatible with being a message.
  subsorts HttpRequest HttpResponse < Content .
  subsort HttpMsg < Msg .
  subsort ResponseToot < ResponseTootList .

  op postRequest  : -> PostRequest .
  op getRequest : -> GetRequest .

  op makeHttpMsg : HttpRequest Address Address -> HttpMsg .
  eq makeHttpMsg(httpRequest, toAddr, fromAddr) =
    (to toAddr from fromAddr : httpRequest) .

  op makeHttpMsg : HttpResponse Address Address -> HttpMsg .
  eq makeHttpMsg(httpResponse, toAddr, fromAddr) =
    (to toAddr from fromAddr : httpResponse) .
  op makeHttpRequest : GetRequest Url -> HttpRequest .
  op makeHttpRequest : GetRequest MasId -> HttpRequest .
  --- Create an HTTP Request for a Hashtag search (GET).
  op makeHttpRequest : GetRequest String -> HttpRequest .
  op makeHttpRequest : GetRequest String Nat -> HttpRequest .
  --- Create an HTTP Request for posting a status or a status with attachment ids (POST).
  op makeHttpRequest : PostRequest String -> HttpRequest .
  --- Create an HTTP Request for posting a status with attachment ids.
  op makeHttpRequest : PostRequest String MasIdList -> HttpRequest .
  --- Create an HTTP Request for posting a media (POST).
  op makeHttpRequest : PostRequest MediaFilename -> HttpRequest .

  --- Create an HTTP Response with a file (returns the file after a get).
  op makeHttpResponse : String MediaFilename -> HttpResponse .
  --- Create an HTTP Response that contains a list of Toots info.
  op makeHttpResponse : String ResponseTootList -> HttpResponse .
  --- Response contains Post Id.
  op makeResponseToot : MasId -> ResponseToot .
  --- Response contains Post Id and Post text.
  op makeResponseToot : MasId String -> ResponseToot .
  --- Response contains Post Id, Post Text and List of Attachment urls.
  op makeResponseToot : MasId String UrlList -> ResponseToot .

  vars httpMsg HTTP_MSG : HttpMsg .
  vars httpRequest HTTP_REQUEST : HttpRequest .
  vars httpResponse HTTP_RESPONSE : HttpResponse .

  op emptyOpStack : -> OpStack [ctor] .
  op _:_ : OpStack OpStack -> OpStack [ctor assoc id: emptyOpStack] .

  op emptyMasIdList : -> MasIdList [ctor] .
  op _:_ : MasIdList MasIdList -> MasIdList [ctor assoc id: emptyMasIdList] .
  
  op emptyResponseTootList : -> ResponseTootList [ctor] .
  op _:_ : ResponseTootList ResponseTootList -> ResponseTootList [ctor assoc id: emptyResponseTootList] .

  op emptyMediaFilenameList : -> MediaFilenameList [ctor] .
  op _::_ : MediaFilenameList MediaFilenameList -> MediaFilenameList [ctor assoc id: emptyMediaFilenameList] .

  op emptyUrlList : -> UrlList [ctor] .
  op _;_ : UrlList UrlList -> UrlList [ctor assoc id: emptyUrlList] .

  op nilOp : -> Op .
  op nilMediaName : -> MediaFilename .
  op nilMasId : -> MasId .
  op nilUrl : -> Url .

  --- Op strings ---
  op GET-STATUS-OP : -> Op .
  eq GET-STATUS-OP = "get_status" .

  op GET-MEDIA-URL-OP : -> Op .
  eq GET-MEDIA-URL-OP = "get_media_from_url" .

  op GET-MEDIA-POST-OP : -> Op .
  eq GET-MEDIA-POST-OP = "get_media_from_post" .

  op GET-MEDIA-HASHTAG-OP : -> Op .
  eq GET-MEDIA-HASHTAG-OP = "get_media_from_hashtag" .

  op POST-MEDIA-OP : -> Op .
  eq POST-MEDIA-OP = "post_media" .

  op POST-STATUS-OP : -> Op .
  eq POST-STATUS-OP = "post_status" .

  op POST-STATUS-W-MEDIA-OP : -> Op .
  eq POST-STATUS-W-MEDIA-OP = "post_status_w_media" .
  
  --- Variables ---
  vars fromAddr toAddr FROM-ADDR TO-ADDR MAS_CLIENT_ADDR MAS_SERVER_ADDR : Address .
  vars masClAttrs masClAttrsNext masClAttrsTrans msgAttrs : AttributeSet .
  vars masClAttrsMsg : AttributeSetMsgs .
  vars postId POST_ID : MasId .
  vars masIdList MAS_ID_LIST : MasIdList .
  vars mediaName MEDIA_NAME : MediaFilename .
  vars mediaFilenameList MEDIA_FILENAME_LIST MEDIA_FILENAME_LIST' : MediaFilenameList .
  vars outMsg : Msg .
  vars LIMIT : Nat .
  vars currentOpStack OP_STACK : OpStack .
  vars currentOp CURRENT_OP : Op .
  vars postStatus POSTING_STATUS STATUS : Op .
  vars responseToot RESPONSE_TOOT : ResponseToot .
  vars responseTootList RESPONSE_TOOT_LIST : ResponseTootList .
  vars hashtag HASHTAG POST_TEXT : String .
  vars mediaLocation url URL : Url .
  vars urlList urlList' URL_LIST : UrlList .

  --------------------
  ---[Mastodon Client]
  --------------------
  --- Attributes ---
  op MasClient : -> ActorType [ctor] .

  --- This Client's address.
  op thisAddr:_ : Address -> Attribute [ctor] .
  --- The Mastodon Server's address.
  op masServerAddr:_ : Address -> Attribute [ctor] .
  --- The program counter, a stack that tracks current composable operations.
  op opStack:_ : OpStack -> Attribute [ctor] .
  --- The attachment / images that should be posted.
  op postingMedia:_ : MediaFilenameList -> Attribute [ctor] .
  --- The media Ids of these attachments.
  op postingMediaIds:_ : MasIdList -> Attribute [ctor] .
  --- The text to post in a toot.
  op postingText:_ : String -> Attribute [ctor] .
  --- The list of URL media / images to get (usually from a post or series of posts).
  op gettingMediaUrls:_ : UrlList -> Attribute [ctor] .
  --- The list of media / images being retreived.
  op gettingMedias:_ : MediaFilenameList -> Attribute [ctor] .
  --- The list of posts to get (usually from a hashtag search).
  op gettingPosts:_ : MasIdList -> Attribute [ctor] .

  --- Eqs ---
  ----- For Messages -----
  op getUrlList : ResponseToot -> UrlList .
  eq getUrlList(makeResponseToot(POST_ID, POST_TEXT, URL_LIST)) = URL_LIST .

  op getUrlList : ResponseTootList -> UrlList .
  eq getUrlList(RESPONSE_TOOT : RESPONSE_TOOT_LIST) =
    (getUrlList(RESPONSE_TOOT) ; getUrlList(RESPONSE_TOOT_LIST))
    .
  eq getUrlList(emptyResponseTootList) = emptyUrlList .

  op getSrcAddr : HttpMsg -> Address .
  eq getSrcAddr(makeHttpMsg(HTTP_REQUEST, TO-ADDR, FROM-ADDR)) = FROM-ADDR .
  eq getSrcAddr(makeHttpMsg(HTTP_RESPONSE, TO-ADDR, FROM-ADDR)) = FROM-ADDR .

  op getHttpResponse : HttpMsg -> HttpResponse .
  eq getHttpResponse(makeHttpMsg(HTTP_RESPONSE, TO-ADDR, FROM-ADDR)) = HTTP_RESPONSE .

  op getHttpRequest : HttpMsg -> HttpRequest .
  eq getHttpRequest(makeHttpMsg(HTTP_REQUEST, TO-ADDR, FROM-ADDR)) = HTTP_REQUEST .

  op getPostId : ResponseToot -> MasId .
  eq getPostId(makeResponseToot(POST_ID)) = POST_ID .
  eq getPostId(makeResponseToot(POST_ID, POST_TEXT)) = POST_ID .
  eq getPostId(makeResponseToot(POST_ID, POST_TEXT, URL_LIST)) = POST_ID .

  op getPostId : ResponseTootList -> MasId .
  eq getPostId(RESPONSE_TOOT : RESPONSE_TOOT_LIST) = getPostId(RESPONSE_TOOT) .

  ----- For Client attributes -----
  op getThisAddr : AttributeSet -> Address .
  eq getThisAddr(masClAttrs, thisAddr: MAS_CLIENT_ADDR) = MAS_CLIENT_ADDR .

  op getMastodonServerAddr : AttributeSet -> Address .
  eq getMastodonServerAddr(masClAttrs, masServerAddr: MAS_SERVER_ADDR) = MAS_SERVER_ADDR . 

  op getPostingMedia : AttributeSet -> MediaFilenameList .
  eq getPostingMedia(masClAttrs, postingMedia: MEDIA_FILENAME_LIST) = MEDIA_FILENAME_LIST .

  op getPostingMediaIds : AttributeSet -> MasIdList .
  eq getPostingMediaIds(masClAttrs, postingMediaIds: MAS_ID_LIST) = MAS_ID_LIST .

  op getPostingText : AttributeSet -> String .
  eq getPostingText(masClAttrs, postingText: POST_TEXT) = POST_TEXT .

  op peekOp : AttributeSet -> Op .
  eq peekOp(masClAttrs, opStack: (CURRENT_OP : OP_STACK)) = CURRENT_OP .
  eq peekOp(masClAttrs) = nilOp [owise] .

  op peekOp : OpStack -> Op .
  eq peekOp((CURRENT_OP : OP_STACK)) = CURRENT_OP .
  eq peekOp(emptyOpStack) = nilOp [owise] .

  op popOp : AttributeSet -> AttributeSet .
  eq popOp(masClAttrs, opStack: (CURRENT_OP : OP_STACK)) = (masClAttrs, opStack: OP_STACK) .
  eq popOp(masClAttrs, opStack: emptyOpStack) = (masClAttrs, opStack: emptyOpStack) .

  op popOp : OpStack -> OpStack .
  eq popOp(CURRENT_OP : OP_STACK) = OP_STACK .

  op pushOp : AttributeSet Op -> AttributeSet .
  eq pushOp((masClAttrs, opStack: OP_STACK), CURRENT_OP) = (masClAttrs, opStack: (CURRENT_OP : OP_STACK)) .

  op pushOp : AttributeSet OpStack -> AttributeSet .
  eq pushOp((masClAttrs, opStack: currentOpStack), OP_STACK) = (masClAttrs, opStack: (currentOpStack : OP_STACK)) .

  op pushOp : OpStack Op -> OpStack .
  eq pushOp(OP_STACK, CURRENT_OP) = (CURRENT_OP : OP_STACK) .

  op popMediaList : MediaFilenameList -> MediaFilenameList .
  eq popMediaList(MEDIA_NAME :: MEDIA_FILENAME_LIST) = MEDIA_FILENAME_LIST .
  eq popMediaList(emptyMediaFilenameList) = emptyMediaFilenameList .

  op popUrlList : UrlList -> UrlList .
  eq popUrlList(URL ; URL_LIST) = URL_LIST .
  eq popUrlList(emptyUrlList) = emptyUrlList .

  op peekMediaList : MediaFilenameList -> MediaFilename .
  eq peekMediaList((MEDIA_NAME :: MEDIA_FILENAME_LIST)) = MEDIA_NAME .
  eq peekMediaList(emptyMediaFilenameList) = nilMediaName .

  op popMediaIdList : MasIdList -> MasIdList .
  eq popMediaIdList(postId : MAS_ID_LIST) = MAS_ID_LIST .
  eq popMediaIdList(emptyMasIdList) = emptyMasIdList .

  op peekMediaIdList : MasIdList -> MasId .
  eq peekMediaIdList(postId : MAS_ID_LIST) = postId .
  eq peekMediaIdList(emptyMasIdList) = nilMasId .

  op peekUrlList : UrlList -> Url .
  eq peekUrlList(url ; URL_LIST) = url .
  eq peekUrlList(emptyUrlList) = nilUrl .

  --- The post method.
  --- TODO: Upgrade to send an actual HTTP request.
  op post : MediaFilename Address Address -> HttpMsg .
  eq post(MEDIA_NAME, toAddr, fromAddr) = 
    makeHttpMsg(makeHttpRequest(postRequest, MEDIA_NAME), toAddr, fromAddr) .

  op post : String Address Address -> HttpMsg .
  eq post(STATUS, toAddr, fromAddr) = 
    makeHttpMsg(makeHttpRequest(postRequest, STATUS), toAddr, fromAddr) .

  op post : String MasIdList Address Address -> HttpMsg .
  eq post(STATUS, MAS_ID_LIST, toAddr, fromAddr) =
    makeHttpMsg(makeHttpRequest(postRequest, STATUS, MAS_ID_LIST), toAddr, fromAddr) .

  --- The get method.
  --- TODO: Upgrade to send an actual HTTP request.
  op get : Url Address Address -> HttpMsg .
  eq get(URL, toAddr, fromAddr) = makeHttpMsg(makeHttpRequest(getRequest, URL), toAddr, fromAddr) .

  op get : MasId Address Address -> HttpMsg .
  eq get(postId, toAddr, fromAddr) = makeHttpMsg(makeHttpRequest(getRequest, postId), toAddr, fromAddr) .

  --- Get all posts with hashtag.
  op get : String Address Address -> HttpMsg .
  eq get(hashtag, toAddr, fromAddr) = makeHttpMsg(makeHttpRequest(getRequest, hashtag), toAddr, fromAddr) .

  --- Get all posts with hashtag but limit their number.
  op get : String Nat Address Address -> HttpMsg .
  eq get(hashtag, LIMIT, toAddr, fromAddr) = makeHttpMsg(makeHttpRequest(getRequest, hashtag, LIMIT), toAddr, fromAddr) .

  ---Upload an attachment---
  op uploadNewMedia : AttributeSet String -> AttributeSetMsgs .
  eq uploadNewMedia((masClAttrs, opStack: OP_STACK), MEDIA_NAME) =
    { (masClAttrs, opStack: (POST-MEDIA-OP : OP_STACK)) , 
      post(MEDIA_NAME, getMastodonServerAddr(masClAttrs), getThisAddr(masClAttrs)) }
    .

  ---Post a new (text) status with or without attachment Ids---
  op uploadNewStatus : AttributeSet String MasIdList -> AttributeSetMsgs .
  ceq uploadNewStatus((masClAttrs, opStack: OP_STACK), STATUS, MAS_ID_LIST) =
    { (masClAttrs, opStack: (POST-STATUS-OP : OP_STACK)) , outMsg }
    if outMsg :=
      (if MAS_ID_LIST == emptyMasIdList
           then post(STATUS, getMastodonServerAddr(masClAttrs), getThisAddr(masClAttrs))
           else post(STATUS, MAS_ID_LIST, getMastodonServerAddr(masClAttrs), getThisAddr(masClAttrs))
       fi)
    .

  ---Post new (text) status with image attachments---
  op uploadNewStatusWImages : AttributeSet String MediaFilenameList -> AttributeSetMsgs .
  ceq uploadNewStatusWImages((masClAttrs, opStack: OP_STACK, postingText: POSTING_STATUS, postingMedia: MEDIA_FILENAME_LIST'), STATUS, (MEDIA_NAME :: MEDIA_FILENAME_LIST)) =
    masClAttrsMsg
      if masClAttrsTrans := (masClAttrs, opStack: (POST-STATUS-W-MEDIA-OP : OP_STACK), postingText: STATUS, postingMedia: MEDIA_FILENAME_LIST)
        /\ masClAttrsMsg := uploadNewMedia(masClAttrsTrans, MEDIA_NAME)
    .

  ---Download media at url---
  op downloadMediaAtUrl : AttributeSet Url -> AttributeSetMsgs .
  eq downloadMediaAtUrl(masClAttrs, mediaLocation) =
    { pushOp(masClAttrs, GET-MEDIA-URL-OP) ,
      get(mediaLocation, getMastodonServerAddr(masClAttrs), getThisAddr(masClAttrs)) }
    .

  ---Download all media in post---
  op downloadMediaAtPost : AttributeSet MasId -> AttributeSetMsgs .
  eq downloadMediaAtPost(masClAttrs, postId) =
    { pushOp(masClAttrs, GET-MEDIA-POST-OP) ,
      get(postId, getMastodonServerAddr(masClAttrs), getThisAddr(masClAttrs)) }
    .

  ---Download all media from posts resulting in hashtag search.
  op downloadMediaAtHashtag : AttributeSet String -> AttributeSetMsgs .
  --- TODO: Replace this with actually parsing the response and filling in the attachment urls to get.
  eq downloadMediaAtHashtag((masClAttrs, gettingMediaUrls: URL_LIST), hashtag) =
    { pushOp((masClAttrs, gettingMediaUrls: emptyUrlList), (GET-STATUS-OP : GET-MEDIA-HASHTAG-OP)) ,
      get(hashtag, getMastodonServerAddr(masClAttrs), getThisAddr(masClAttrs)) }
    .

  --- Handle a reply from the server, for a media posting request.
  op handleReply : HttpMsg AttributeSet -> AttributeSetMsgs .
  eq handleReply(HTTP_MSG, masClAttrs) =
    handleReply(getHttpResponse(HTTP_MSG), masClAttrs)
    .

  ---              MsgAttributes ClientAttributes
  op handleReply : HttpResponse AttributeSet -> AttributeSetMsgs .
  ceq handleReply(makeHttpResponse("OK", RESPONSE_TOOT_LIST), masClAttrs) =
    handleMediaPostReply(getPostId(RESPONSE_TOOT_LIST), masClAttrs)
      if peekOp(masClAttrs) == POST-MEDIA-OP
    .

  --- Handle a reply from the server, for a status posting request.
  ---              MsgAttributes ClientAttributes
  ceq handleReply(makeHttpResponse("OK", RESPONSE_TOOT_LIST), masClAttrs) =
    { handleStatusPostReply(getPostId(RESPONSE_TOOT_LIST), masClAttrs), nullMsg }
      if peekOp(masClAttrs) == POST-STATUS-OP
    .

  --- Handle a reply from the server, for a statuses by hashtag get request.
  ---              MsgAttributes ClientAttributes
  ceq handleReply(makeHttpResponse("OK", RESPONSE_TOOT_LIST), masClAttrs) =
    handleStatusGetReply(RESPONSE_TOOT_LIST, popOp(masClAttrs))
      if peekOp(masClAttrs) == GET-STATUS-OP
    .

  --- Handle a reply from the server for a get media by URL request.
  ---              MsgAttributes ClientAttributes
  ceq handleReply(makeHttpResponse("OK", MEDIA_NAME), masClAttrs) =
    handleMediaGetByUrl(MEDIA_NAME, masClAttrs)
      if peekOp(masClAttrs) == GET-MEDIA-URL-OP
    .

  --- Handle a reply from the server, for a get media by Id request.
  ---              MsgAttributes ClientAttributes
  ceq handleReply(makeHttpResponse("OK", RESPONSE_TOOT_LIST), masClAttrs) =
    handleMediaGetById(getUrlList(RESPONSE_TOOT_LIST), masClAttrs)
      if peekOp(masClAttrs) == GET-MEDIA-POST-OP
    .

  --- Handle a Media posting reply.
  ---                       MsgAttributes ClientAttributes  ClientAttributes
  op handleMediaPostReply : MasId AttributeSet -> AttributeSetMsgs .
  eq handleMediaPostReply(POST_ID, (masClAttrs, opStack: OP_STACK, postingMediaIds: masIdList)) =
    cueNextPostOp(masClAttrs, opStack: popOp(OP_STACK), postingMediaIds: (masIdList : POST_ID))
    .

  --- Handle a Status posting reply.
  ---                       MsgAttributes ClientAttributes  ClientAttributes
  op handleStatusPostReply : MasId AttributeSet -> AttributeSet .
  eq handleStatusPostReply(POST_ID, (masClAttrs, opStack: OP_STACK, postingMediaIds: MAS_ID_LIST)) =
    (masClAttrs, opStack: popOp(OP_STACK), postingMediaIds: emptyMasIdList)
    .
    
  --- Finish handling a reply from the server.
  op cueNextPostOp : AttributeSet -> AttributeSetMsgs .
  ceq cueNextPostOp(masClAttrs, postingMedia: MEDIA_FILENAME_LIST) =
    masClAttrsMsg
      *** Get the next media name that might need to be uploaded.
      if mediaName := peekMediaList(MEDIA_FILENAME_LIST)
           *** Prepare a pop of the media list.
        /\ mediaFilenameList := popMediaList(MEDIA_FILENAME_LIST)
           *** Get the current op from the stack.
        /\ currentOp := peekOp(masClAttrs)
        /\ masClAttrsMsg := 
            (if currentOp == POST-STATUS-W-MEDIA-OP
            *** If posting a status with attachments, check if there are attachments left to upload.
              then (if mediaName == nilMediaName
                      *** If no more attachment to upload, upload status with media Ids.
                      then uploadNewStatus((masClAttrs, postingMedia: emptyMediaFilenameList), getPostingText(masClAttrs), getPostingMediaIds(masClAttrs, postingMedia: MEDIA_FILENAME_LIST))
                      *** If attachments left to upload, upload the next one.
                      else uploadNewMedia((masClAttrs, postingMedia: mediaFilenameList), mediaName)
                   fi)
              *** Otherwise, done.
              else {(masClAttrs, postingMedia: MEDIA_FILENAME_LIST), nullMsg}
           fi)
    .

  --- Handle a Get response from a get toot or hashtag search.
  ---                      MsgAttributes ClientAttributes  ClientAttributes
  op handleStatusGetReply : ResponseTootList AttributeSet -> AttributeSetMsgs .
  ceq handleStatusGetReply(RESPONSE_TOOT_LIST, (masClAttrs, opStack: OP_STACK, gettingMediaUrls: URL_LIST)) =
    masClAttrsMsg
      if currentOp := peekOp(OP_STACK)
        /\ (url ; urlList) := getUrlList(RESPONSE_TOOT_LIST)
        /\ masClAttrsMsg := 
          (if currentOp == GET-MEDIA-HASHTAG-OP
          *** If currently downloading all media related to hashtag
            then (if url == nilUrl
                  then {(masClAttrs, opStack: popOp(OP_STACK), gettingMediaUrls: emptyUrlList), nullMsg} *** TODO: Replace this with a call to reassemble the media together into a message from Alice.
                  else downloadMediaAtUrl((masClAttrs, opStack: OP_STACK, gettingMediaUrls: urlList), url)
                 fi)
            *** Then download the media at next url.
            else {(masClAttrs, opStack: popOp(OP_STACK), gettingMediaUrls: URL_LIST), nullMsg}
            *** Else we are done.
          fi)
    .

  --- Handle a Get media in post by Url reply.
  ---                      MsgAttributes ClientAttributes  ClientAttributes
  op handleMediaGetByUrl : MediaFilename AttributeSet -> AttributeSetMsgs .
  eq handleMediaGetByUrl(MEDIA_NAME, (masClAttrs, opStack: OP_STACK, gettingMediaUrls: URL_LIST, gettingMedias: mediaFilenameList)) =
    cueNextGetOp((masClAttrs, gettingMedias: (mediaFilenameList :: MEDIA_NAME)), opStack: popOp(OP_STACK), gettingMediaUrls: URL_LIST)
    .

  --- Handle a Get media in post by Id reply.
  ---                  MsgAttributes ClientAttributes  ClientAttributes
  op handleMediaGetById : UrlList AttributeSet -> AttributeSetMsgs .
  ceq handleMediaGetById(urlList, (masClAttrs, opStack: OP_STACK, gettingMediaUrls: URL_LIST)) =
    masClAttrsMsg
      if url := peekUrlList(urlList)
        /\ urlList' := popUrlList(urlList)
        /\ masClAttrsMsg := downloadMediaAtUrl((masClAttrs, opStack: OP_STACK, gettingMediaUrls: urlList'), url)
    .

  op cueNextGetOp : AttributeSet -> AttributeSetMsgs .
  ceq cueNextGetOp(masClAttrs, opStack: (currentOp : OP_STACK), gettingMediaUrls: URL_LIST) =
    masClAttrsMsg
      if url := peekUrlList(URL_LIST)
        /\ urlList := popUrlList(URL_LIST)
        /\ masClAttrsMsg := 
            (if currentOp == GET-MEDIA-POST-OP
              then (if url == nilUrl
                      then finishGetMediaFromPost(masClAttrs, opStack: OP_STACK, gettingMediaUrls: emptyUrlList)
                      else downloadMediaAtUrl((masClAttrs, opStack: (currentOp : OP_STACK), gettingMediaUrls: urlList), url)
                   fi)
              else finishGetMediaFromPost(masClAttrs, opStack: (currentOp : OP_STACK), gettingMediaUrls: URL_LIST)
            fi)
    .

  op finishGetMediaFromPost : AttributeSet -> AttributeSetMsgs .
  ceq finishGetMediaFromPost(masClAttrs, opStack: (currentOp : OP_STACK), gettingMediaUrls: URL_LIST) =
    masClAttrsMsg
      if url := peekUrlList(URL_LIST)
      /\ masClAttrsMsg :=
         (if currentOp == GET-MEDIA-HASHTAG-OP
            then (if url == nilUrl
                   then {(masClAttrs, opStack: OP_STACK, gettingMediaUrls: emptyUrlList), nullMsg}
                   else downloadMediaAtUrl((masClAttrs, opStack: (currentOp : OP_STACK), gettingMediaUrls: popUrlList(URL_LIST)), url)
                 fi)
            else {(masClAttrs, opStack: (currentOp : OP_STACK), gettingMediaUrls: emptyUrlList), nullMsg}
         fi)
    .

  --------------------
  ---
  --------------------

  --------------------
  ---[Mastodon Server]
  --------------------
  --- Attributes ---
  op MasServer : -> ActorType [ctor] .

  --- This Server's address.
  op thisAddr:_ : Address -> Attribute [ctor] .

  op currentMapId:_ : Nat -> Attribute [ctor] .
  op tootMap:_ : IdTootMap -> Attribute [ctor] .

  op mediaMap:_ : IdMediaMap -> Attribute [ctor] .

  vars masServerAttrs masServerAttrsNext masServerAttrsTrans : AttributeSet .
  vars idTootMap ID_TOOT_MAP : IdTootMap .
  vars masServerResponse : HttpMsg .
  vars idMediaMap ID_MEDIA_MAP : IdMediaMap .
  vars mapId CURRENT_MAP_ID : Nat .
  vars toot TOOT : Toot .

  ------------
  --- Toot Map
  ------------
  sorts Toot .
  sorts IdToot IdTootMap .

  subsorts IdToot < IdTootMap .

  op makeToot : -> Toot .
  op makeToot : String -> Toot .
  op makeToot : String UrlList -> Toot .

  op _~>_ : Nat Toot -> IdToot [ctor] .
  op emptyIdTootMap : -> IdTootMap .
  op _:_ : IdTootMap IdTootMap -> IdTootMap [ctor assoc comm id: emptyIdTootMap] .
  
  op insertToot : IdTootMap Nat Toot -> IdTootMap .
  --- IDs should be unique; do not replace existing entries.
  eq insertToot(idTootMap, CURRENT_MAP_ID, TOOT) =
   idTootMap : (CURRENT_MAP_ID ~> TOOT)
   .

  op insertToot : AttributeSet Toot -> AttributeSet .
  eq insertToot((masServerAttrs, currentMapId: mapId, tootMap: idTootMap), TOOT) =
    (masServerAttrs, currentMapId: s mapId, tootMap: insertToot(idTootMap, s mapId, TOOT))
    .

  ----------------------
  --- Attachment Map ---
  ----------------------
  sorts IdMedia IdMediaMap .
  subsorts IdMedia < IdMediaMap .

  op _~>_ : Nat MediaFilename -> IdMedia [ctor] .
  op emptyIdMediaMap : -> IdMediaMap .
  op _:_ : IdMediaMap IdMediaMap -> IdMediaMap [ctor assoc comm id: emptyIdMediaMap] .

  op insertMedia : IdMediaMap Nat MediaFilename -> IdMediaMap .
  eq insertMedia(idMediaMap, CURRENT_MAP_ID, MEDIA_NAME) =
    idMediaMap : (CURRENT_MAP_ID ~> MEDIA_NAME)
    .

  op insertMedia : AttributeSet MediaFilename -> AttributeSet .
  eq insertMedia((masServerAttrs, currentMapId: mapId, mediaMap: idMediaMap), MEDIA_NAME) =
    (masServerAttrs, currentMapId: s mapId, mediaMap: insertMedia(idMediaMap, s mapId, MEDIA_NAME))
    .

  op getMedia : IdMediaMap Nat -> MediaFilename .
  eq getMedia((idMediaMap : (mapId ~> MEDIA_NAME)), mapId) = MEDIA_NAME .

  op getUrlList : IdMediaMap MasIdList -> UrlList .
  eq getUrlList(idMediaMap, (MAS_ID_LIST : postId)) =
    (getUrlList(idMediaMap, MAS_ID_LIST) ; getMedia(idMediaMap, postId))
    .

  op getUrlList : AttributeSet MasIdList -> UrlList .
  eq getUrlList((masServerAttrs, mediaMap: idMediaMap), MAS_ID_LIST) =
    getUrlList(idMediaMap, MAS_ID_LIST)
    .

  --- Handle a request from the client.
  op handleRequest : HttpMsg AttributeSet -> AttributeSetMsgs .
  eq handleRequest(HTTP_MSG, masServerAttrs) =
    handleRequest(getSrcAddr(HTTP_MSG), getHttpRequest(HTTP_MSG), masServerAttrs)
    .

  op handleRequest : Address HttpRequest AttributeSet -> AttributeSetMsgs .
  eq handleRequest(MAS_CLIENT_ADDR, makeHttpRequest(postRequest, POST_TEXT, MAS_ID_LIST), masServerAttrs) =
    handleStatusPostRequest(MAS_CLIENT_ADDR, POST_TEXT, MAS_ID_LIST, masServerAttrs)
    .

  op handleStatusPostRequest : Address String AttributeSet -> AttributeSetMsgs .
  ceq handleStatusPostRequest(MAS_CLIENT_ADDR, POST_TEXT, masServerAttrs) =
    { masServerAttrsNext, httpMsg }
    if masServerAttrsNext := insertToot(masServerAttrs, makeToot(POST_TEXT))
      /\ (masServerAttrsTrans, currentMapId: CURRENT_MAP_ID) := masServerAttrsNext
      /\ httpMsg :=  makeHttpMsg(makeHttpResponse("OK", makeResponseToot(CURRENT_MAP_ID)), getThisAddr(masServerAttrs), MAS_CLIENT_ADDR)
    .    

  op handleStatusPostRequest : Address String MasIdList AttributeSet -> AttributeSetMsgs .
  ceq handleStatusPostRequest(MAS_CLIENT_ADDR, POST_TEXT, MAS_ID_LIST, masServerAttrs) =
    { masServerAttrsNext, httpMsg }
    if urlList := getUrlList(masServerAttrs, MAS_ID_LIST)
      /\ masServerAttrsNext := insertToot(masServerAttrs, makeToot(POST_TEXT, urlList))
      /\ (masServerAttrsTrans, currentMapId: CURRENT_MAP_ID) := masServerAttrsNext
      /\ httpMsg :=  makeHttpMsg(makeHttpResponse("OK", makeResponseToot(CURRENT_MAP_ID)), getThisAddr(masServerAttrs), MAS_CLIENT_ADDR)
    . 

  --- TODO: Do owize for fail
endm

mod MASTODON_TEST is
  inc MASTODON_NODE .

  ops masClTestAttrs masServerTestAttrs : -> AttributeSet .
  ops masClTestAttrsMsg masServerTestAttrsMsg: -> AttributeSetMsgs .
  ops TEST-TO-ADDR TEST-FROM-ADDR : -> Address .
endm

---set trace on .
---eof

*** Check the behavior of the Op stack.
red peekOp(masClTestAttrs, opStack: POST-MEDIA-OP) == POST-MEDIA-OP .
red peekOp(masClTestAttrs, opStack: emptyOpStack) == nilOp .
red popMediaList(emptyMediaFilenameList) == emptyMediaFilenameList .

*** Check the behavior of the responses.
red getUrlList(makeResponseToot(88, "Post text", ("url0.com" ; "url1.com"))) == ("url0.com" ; "url1.com") .
red getUrlList((makeResponseToot(88, "Post 88 text", ("url0.com" ; "url1.com"))) : (makeResponseToot(109, "Post 109 text", ("url2.com")))) == ("url0.com" ; "url1.com" ; "url2.com") .

*** Clean upload of file media.
red uploadNewMedia((masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: emptyOpStack), "img0.jpg")
  == { (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: POST-MEDIA-OP), 
       (to TEST-TO-ADDR from TEST-FROM-ADDR : makeHttpRequest(postRequest, "img0.jpg")) }
  .

*** Test a media post reply (no other context).
red handleReply(makeHttpResponse("OK", makeResponseToot(445)), (masClTestAttrs, opStack: (POST-MEDIA-OP : emptyOpStack), postingMedia: emptyMediaFilenameList, postingMediaIds: emptyMasIdList))
  == { (masClTestAttrs, opStack: emptyOpStack, postingMedia: emptyMediaFilenameList, postingMediaIds: 445),
       nullMsg }
  .

*** Clean upload of status.
red uploadNewStatus((masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: emptyOpStack), "This is a new toot", emptyMasIdList)
  == { (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: POST-STATUS-OP),
       (to TEST-TO-ADDR from TEST-FROM-ADDR : makeHttpRequest(postRequest, "This is a new toot")) }
  .
*** Clean upload of status with media Ids.
red uploadNewStatus((masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: emptyOpStack), "This is a new toot", (45 : 98))
  == { (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: POST-STATUS-OP),
       (to TEST-TO-ADDR from TEST-FROM-ADDR : makeHttpRequest(postRequest, "This is a new toot", (45 : 98))) }
  .

*** Test a clean status post reply (no other context).
red handleReply(makeHttpResponse("OK", makeResponseToot(445)), (masClTestAttrs, postingMediaIds: (45 : 98), opStack: (POST-STATUS-OP : emptyOpStack)))
  == { (masClTestAttrs, postingMediaIds: emptyMasIdList, opStack: emptyOpStack),
       nullMsg }
  .

*** Test a status with media post.
red uploadNewStatusWImages((masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: emptyOpStack, postingText: "", postingMedia: emptyMediaFilenameList), "This is a new toot with image attachments", "img0.jpg")
  == { (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: (POST-MEDIA-OP : POST-STATUS-W-MEDIA-OP), postingText: "This is a new toot with image attachments", postingMedia: emptyMediaFilenameList),
       (to TEST-TO-ADDR from TEST-FROM-ADDR : makeHttpRequest(postRequest, "img0.jpg")) }
  .

*** Test a media post reply in the context of a status upload with media attachments when there is no media left to upload.
red handleReply(makeHttpResponse("OK", makeResponseToot(445)), (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: (POST-MEDIA-OP : POST-STATUS-W-MEDIA-OP), postingMedia: emptyMediaFilenameList, postingMediaIds: emptyMasIdList, postingText: "Toot with media"))
  == { (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: (POST-STATUS-OP : POST-STATUS-W-MEDIA-OP), postingMedia: emptyMediaFilenameList, postingMediaIds: 445, postingText: "Toot with media"),
       (to TEST-TO-ADDR from TEST-FROM-ADDR : makeHttpRequest(postRequest, "Toot with media", 445)) }
  .

*** Test a media post reply in the context of a status upload with media attachments when there is one media left to upload.
red handleReply(makeHttpResponse("OK", makeResponseToot(445)), (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: (POST-MEDIA-OP : POST-STATUS-W-MEDIA-OP), postingMedia: "img1.jpg", postingMediaIds: emptyMasIdList))
  == { (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: (POST-MEDIA-OP : POST-STATUS-W-MEDIA-OP), postingMedia: emptyMediaFilenameList, postingMediaIds: 445),
       (to TEST-TO-ADDR from TEST-FROM-ADDR : makeHttpRequest(postRequest, "img1.jpg")) }
  .

*** Get a media at a given URL.
red downloadMediaAtUrl((masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: emptyOpStack), "www.media_location.com")
  == { (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: GET-MEDIA-URL-OP),
       (to TEST-TO-ADDR from TEST-FROM-ADDR : makeHttpRequest(getRequest, "www.media_location.com")) }
  .

*** Get all media from a post Id.
red downloadMediaAtPost((masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: emptyOpStack), 446)
  == { (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: GET-MEDIA-POST-OP),
       (to TEST-TO-ADDR from TEST-FROM-ADDR : makeHttpRequest(getRequest, 446)) }
  .

*** Get all media from a hashtag search.
red downloadMediaAtHashtag((masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: emptyOpStack, gettingMediaUrls: "url0"), "cat")
  == { (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: (GET-STATUS-OP : GET-MEDIA-HASHTAG-OP), gettingMediaUrls: emptyUrlList),
       (to TEST-TO-ADDR from TEST-FROM-ADDR : makeHttpRequest(getRequest, "cat")) }
  .

*** Test a status get reply in the context of an all-media download via hashtag search.
red handleReply(makeHttpResponse("OK", makeResponseToot(88, "Post 88 text", ("url0.com" ; "url1.com" ; "url2.com"))),
                (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: (GET-STATUS-OP : GET-MEDIA-HASHTAG-OP), gettingMediaUrls: emptyUrlList))
  == { (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: (GET-MEDIA-URL-OP : GET-MEDIA-HASHTAG-OP), gettingMediaUrls: ("url1.com" ; "url2.com")),
       (to TEST-TO-ADDR from TEST-FROM-ADDR : makeHttpRequest(getRequest, "url0.com")) }
  .

*** Test a media get reply (from a post id).
red handleReply(makeHttpResponse("OK", makeResponseToot(88, "Post 88 text", ("url0.com" ; "url1.com" ; "url2.com"))), (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: GET-MEDIA-POST-OP, gettingMediaUrls: emptyUrlList))
  == { (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: (GET-MEDIA-URL-OP : GET-MEDIA-POST-OP), gettingMediaUrls: ("url1.com" ; "url2.com")),
       (to TEST-TO-ADDR from TEST-FROM-ADDR : makeHttpRequest(getRequest, "url0.com")) }
  .

*** Test a media get reply (from a URL) during the process of getting all media from all posts with a certain hashtag.
*** There is only one post with the hashtag, and only one media to retreive.
red handleReply(makeHttpResponse("OK", "img0.jpg"), (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: (GET-MEDIA-URL-OP : GET-MEDIA-HASHTAG-OP), gettingMediaUrls: emptyUrlList, gettingMedias: emptyMediaFilenameList))
  == { (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: emptyOpStack, gettingMediaUrls: emptyUrlList, gettingMedias: "img0.jpg"),
      nullMsg }
  .

*** Test a media get reply (from a URL) during the process of getting all media from all posts with a certain hashtag.
*** There is only one post with the hashtag, and three media to retreive.
red handleReply(makeHttpResponse("OK", "img0.jpg"), (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: (GET-MEDIA-URL-OP : GET-MEDIA-HASHTAG-OP), gettingMediaUrls: ("url1" ; "url2"), gettingMedias: emptyMediaFilenameList))
  == { (masClTestAttrs, thisAddr: TEST-FROM-ADDR, masServerAddr: TEST-TO-ADDR, opStack: (GET-MEDIA-URL-OP : GET-MEDIA-HASHTAG-OP), gettingMediaUrls: "url2", gettingMedias: "img0.jpg"),
       (to TEST-TO-ADDR from TEST-FROM-ADDR : makeHttpRequest(getRequest, "url1")) }
  .

*** Test handling a status post with media Ids.
red handleRequest(TEST-FROM-ADDR,
                  makeHttpRequest(postRequest, "A new toot with attachments", (88 : 46)),
                  (masServerTestAttrs, currentMapId: 10, tootMap: emptyIdTootMap, mediaMap: ((88 ~> "img0.jpg") : (46 ~> "img1.jpg"))))
  .

