--- MAUDE_HCS: maude_hcs
---
--- Software Markings (UNCLASS)
--- Maude-HCS Software
---
--- Copyright (C) 2025 RTX BBN Technologies Inc. All Rights Reserved
---
--- The computer software and computer software documentation are licensed
--- under the Apache License, Version 2.0 (the "License"); you may not use
--- this file except in compliance with the License. A copy of the License
--- is provided in the LICENSE file, but you may obtain a copy of the
--- License at:  https://www.apache.org/licenses/LICENSE-2.0
---
--- The computer software and computer software documentation are based
--- upon work supported by the Defense Advanced Research Projects Agency (DARPA)
--- under Agreement No. HR00l 12590083.
---
--- This document does not contain technology or technical data controlled under
--- either the U.S. International Traffic in Arms Regulations or the U.S. Export
--- Administration Regulations.
---
--- DISTRIBUTION STATEMENT A: Approved for public release; distribution is
--- unlimited.
---
--- Notice: Markings. Any reproduction of this computer software, computer
--- software documentation, or portions thereof must also reproduce the markings
--- contained herein. Refer to the provided NOTICE file.
---
--- MAUDE_HCS: end


load ../../../deps/dns_formalization/Maude/dns/probabilistic-model/dns
load ../../common/maude/_aux
load ../../raceboat/rb-integration

mod CP2_APP is
  inc DNS .
  inc CP2_NODE .
  inc RB-CLIENT + RB-SERVER .

  vars aliceRbClient : Address .
  vars attrs attrsTemp attrsNext : AttributeSet .
  vars aliceAddr bobAddr ALICE_ADDR BOB_ADDR CONTENT_MGR_ADDR USER_MODEL_ADDR : Address .
  vars encryptedContract CC_FILE CONTRACT : ByteSeq .
  vars contract contractKey contractHash contractHashtag CONTRACT_KEY : ByteSeq .
  vars CONTRACT_LIST : ByteSeqL .
  vars T : Float . ---timestamps
  vars contractId : Nat .


  -------------------
  --- Alice Rules ---
  -------------------
  crl [alice-send-contract]:
    < ALICE_ADDR : TxApp | attrs, contractCount: contractId, queue: (CONTRACT :: CONTRACT_LIST) >
    {T, (to ALICE_ADDR : start)}
  =>
    < ALICE_ADDR : TxApp | attrs, contractCount: s contractId, queue: CONTRACT_LIST >
    startRbClient(0., getRbUserModelAddr(attrs))
    [0.0002, (to getRbContentMgrAddr(attrs) from ALICE_ADDR : pkg(encryptedContract, contractHashtag)), 0] 
    --- TODO: Send using the proper delays.
    [0.2, (to getIodineClientAddr(attrs) from ALICE_ADDR : ccFile(contractKey, contractHash, contractHashtag)), 0]
    if contractKey := key(contractId, 4)
      /\ contractHash := hash(CONTRACT, 4)
      --- TODO: Change to the right hashtag generation.
      /\ contractHashtag := wHashTag(contractId)
      /\ encryptedContract := encrypt(CONTRACT, contractKey)
  [ print "(" T ") Alice start, sending contract #" contractId " <<<< " CONTRACT " >>>> with key " contractKey ]
  .


  -----------------
  --- Bob Rules ---
  -----------------
  rl [bob-recv-cc-file]:
    < BOB_ADDR : RxApp | (attrs, currentKey: CONTRACT_KEY) >
    {T, (to BOB_ADDR : CC_FILE)}
  =>
    < BOB_ADDR : RxApp | (attrs, currentKey: readKey(CC_FILE)) >
    [0.0, (to getRbContentMgrAddr(attrs) from BOB_ADDR : pkg(getHashtagAsByteSeq(CC_FILE))), 0]
  [ print "(" T ") Bob received CC file " CC_FILE ]
  .

  rl [bob-start]:
    < BOB_ADDR : RxApp | attrs >
    {T, (to BOB_ADDR : start)}
  =>
    < BOB_ADDR : RxApp | attrs >
    startRbServer(0., getRbUserModelAddr(attrs))
  [ print "(" T ") Bob start." ]
  .

  crl [bob-recv-encr-file]:
    < BOB_ADDR : RxApp | attrs >
    {T, (to BOB_ADDR from CONTENT_MGR_ADDR : pkg(encryptedContract, contractHash))}
  =>
    < BOB_ADDR : RxApp | attrs >
    if contract := decrypt(encryptedContract, getCurrentKey(attrs))
  [ print "(" T ") Bob received encrypted file, decrypted with key >>>> " contract " <<<<" ]
  .

endm

