load ../../../deps/dns_formalization/Maude/src/nondet-model/_aux

mod MYLISTS is
  pr AUX .

  ---- Lists of messages (sent or received by apps)
  sort MsgList .  
  subsort Msg < MsgList .

  var msg : Msg .
  var msgL : MsgList .
  
  op nilMsgList : -> MsgList [ctor] .
  op _;_ : MsgList MsgList -> MsgList [ctor assoc id: nilMsgList] .

  op length : MsgList -> Nat .
  eq length(nilMsgList) = 0 .
  eq length(msg ; msgL) = s length(msgL) .
  
  ---- Alice sends "UDP packets" to the TUN IFace
  ---- Each packet has a data len (application bytes being sent), and an id for tracking
  ---- The len determines how many "fragments" are needed to send the packet over DNS
  ---- A packet gets fragmented into 1 or more fragments
  ---- A fragment is what gets embedded into a DNS query
  ----   recall subsort Nat < Id 
  
  sorts Packet Fragment FragmentList .
  subsort Fragment < FragmentList .

  var FL : FragmentList .
  var F : Fragment .

  op mtfl    : -> FragmentList [ctor] .
  op _:_    : FragmentList FragmentList -> FragmentList [ctor assoc id: mtfl] .

  op length : FragmentList -> Nat .     
  
  eq length(mtfl) = 0 .
  eq length(F : FL) = s length(FL) .

endm

view FragmentList from TRIV to MYLISTS is
  sort Elt to FragmentList .
endv

mod IODINE_SORTS is   
  pr MYLISTS .
---  inc META-LEVEL .
---  pr CONVERSION .
  
  vars ALICE BOB : Address . 
  var NAME : Name .
  vars N N1 N2 N3 P_LEN : Nat .
  var B B1 : Bool .  
 

  subsorts Packet Fragment < Content . 
  ----        AliceAddr Packet# DataLenBytes  
  op packet : Address Nat Nat -> Packet .
  ----          AliceAddr  Packet# Frag# DataLenBytes lastFrag
  op fragment : Address Nat Nat Nat Bool -> Fragment .  
  --- convert a fragment into a Name to be embedded
  op nm : Fragment -> Name .  
  --- convert back to fragment
  op fragment : Name -> Fragment .    
  op nilFragment : -> Fragment .

  --- convert a fragment into a Qid to be embedded
  --- op toQId : Fragment -> Qid .
  --- op toQId : Qid Nat Nat Nat -> Qid .
  --- eq toQId(fragment(QID1, N, N1, N2)) = toQId(QID1, N, N1, N2) .
  --- eq toQId(QID1, N, N1, N2) = qid(string(QID1) + "#" + string(N,10) + "#" + string(N1, 10) + "#" + string(N2, 10)) .
  
  eq fragment(nm(fragment(ALICE, N, N1, N2, B))) = fragment(ALICE, N, N1, N2, B) .
  eq fragment(NAME) = nilFragment [owise] .

  op getPacketLength : Packet -> Nat .
  eq getPacketLength(packet(ALICE, N, P_LEN)) = P_LEN .


endm  
--- these views allow us to create lists LIST{X} where X is sort Packet or Fragment
--- view Packet from TRIV to IODINE_SORTS is sort Elt to Packet . endv
--- view Fragment from TRIV to IODINE_SORTS is sort Elt to Fragment . endv



mod IODINE_AUX is
  pr IODINE_SORTS .
  --- pr LIST{Packet} .
---  pr LIST{Fragment} .
  
  sorts MapIdMsg  IdMsg .
  subsort IdMsg < MapIdMsg .
  
  vars N N1 N2 N3 P_LEN : Nat .
  var B B1 LAST_FRAG : Bool .
  ---var curFragments : List{Fragment} .
  var curFragments : FragmentList .
  var Pkt Pkt1 : Packet .
  var Frag Frag1 : Fragment .
  var ALICE : Address .

  var wdb : MapIdMsg .
  var ID : Id .
  var msg msg0 : Msg .

  op _<~_ : Id Msg -> IdMsg [ctor] .
  op mtIdMsg : -> MapIdMsg .
  op  _!!_ : MapIdMsg MapIdMsg -> MapIdMsg 
                 [ctor assoc comm id: mtIdMsg] .
  op updateMap : MapIdMsg Id Msg -> MapIdMsg .
  eq updateMap(wdb !! (ID <~ msg0), ID, msg) 
          = wdb !! (ID <~ msg) .
  eq updateMap(wdb,ID,msg) = wdb !! (ID <~ msg) [owise] .

  ----        return list of fragments that form a packet
  ----          Packet HOSTNAME_LEN
  op makeFragments : Packet Nat -> FragmentList .
  ----          Packet HOSTNAME_LEN cur_frag_num cur_fragments
  op $makeFragments : Packet Nat Nat FragmentList -> FragmentList .
  
  eq makeFragments(packet(ALICE, N, N1), N2) = 
    $makeFragments(packet(ALICE, N, N1), N2, 0, mtfl) .
  
  eq $makeFragments(packet(ALICE, N, N1), N2,  N3, curFragments) =
    if N1 > N2 
    then
      $makeFragments(packet(ALICE, N, (N1 - N2)), N2, s N3, curFragments : fragment(ALICE, N, s N3, N2, false)) 
    else 
      curFragments : fragment(ALICE, N, s N3, N2, true)
    fi  
    .

endm

mod IODINE_NODE is
  inc IODINE_AUX .

  var wattrs : AttributeSet .
  var msg : Msg .
  var NAME NAME1 : Name .
  var F : Fragment .
  var ADDR ADDR1 AliceAddr BobAddr : Address .
  var ID : Id .
  var QTYPE : RType .
  var NUM_FRAGS : Nat .
  vars SEQ_CTR CUR_FRAG : Nat .
  vars N P_LEN HOSTNAME_LEN : Nat .
  var FRAGMENT_LIST : FragmentList .
  var db wDb : List{Record} .


--------  Nesting/Meta configuration  attribute
  op conf:_ : Config -> Attribute [ctor] .
  
  --------------------------------
  --- [App Bob] --- 
  ---   Bob receives weird messages from WNameserver
  --------------------------------
  op RcvApp : -> ActorType [ctor] .
  op rcvd:_ : MsgList  -> Attribute [ctor] .

  **** constructor for making receiver app actor
  op mkRcvApp : Address  -> Actor [ctor] .  
  eq mkRcvApp(BobAddr) =
    < BobAddr : RcvApp | rcvd: nilMsgList > .  

  ----------------
  --- [WCLIENT] ---  Client wrapper  -- alternate view
**** wclient = < clId : WDNSClient | wattrs 
****              conf: (< appId : WApp | attrs >  msgs) ?
**** contains app used by Alice to generate hidden msgs
----------------
  op WCL : -> ActorType [ctor] .

  --- the DNS resolver - should be know to weird client
  op resv:_ : Address -> Attribute [ctor] . 
  --- the base weird name to which hidden msg is attached
  op wDom:_ : Name -> Attribute [ctor] .
  --- store info about query  sent, pending response  
  op wDb:_ : MapIdMsg -> Attribute [ctor] .
  --- increment counter for each DNS query sent
  op queryCtr:_ : Nat -> Attribute [ctor] .
  --- increment counter for each packet being sent
  op seqCtr:_ : Nat -> Attribute [ctor] .
  --- increment counter for each packet fragment being sent
  op fragmentCtr:_ : Nat -> Attribute [ctor] .
  --- fragment list, its size, and the current fragment being sent
  op fragments:_ : FragmentList -> Attribute [ctor] .  
  op fragmentsSize:_ : Nat -> Attribute [ctor] .
  op currFragment:_ : Nat -> Attribute [ctor] .
  --- number of retransmissions of current fragment
  op numAttempts:_ : Nat -> Attribute [ctor] . 

  op getResolver : AttributeSet ~> Address .
  eq getResolver(wattrs, resv: ADDR) = ADDR .

  op isSending : AttributeSet -> Bool .
  eq isSending(wattrs, fragmentsSize: NUM_FRAGS) =
    if NUM_FRAGS > 1
      then true 
    else
      false
    fi
    .
  eq isSending(wattrs) = false [owise] .

  --- Handle a new packet (from Alice)
  ---                   wClient Attributes Packet Max hostname length
  op handleNewPacket : AttributeSet Packet Nat -> AttributeSet .
  ceq handleNewPacket((wattrs, seqCtr: SEQ_CTR), packet(ADDR, N, P_LEN), HOSTNAME_LEN) =
    wattrs, (seqCtr: s SEQ_CTR), (currFragment: 0), (numAttempts: 0), (fragments: FRAGMENT_LIST), (fragmentsSize: length(FRAGMENT_LIST)), (fragmentCtr: 0)
      if FRAGMENT_LIST := makeFragments(packet(ADDR, N, P_LEN), HOSTNAME_LEN) . 
  eq handleNewPacket(wattrs) = wattrs [owise] .

---  crl [iClientEmbedding]:
---    < CL : WCL | wattrs, conf : (appA (to ADDR' from CL : wc(wmsg) msgs) >
---
---    => < CL : WCL | wattrs0, conf : (appA msgs) >
---    if {wattrs0, msg} := embed(CL, wattrs, (to ADDR' from CL : wc(wmsg)))
---    [print "iClientEmbedding msg "  msg]
---    .

  --------------------------------
  --- [WNameserver] --- Weird/wrapped Nameserver
  --------------------------------
  op WNameserver : -> ActorType [ctor] .  
  --- This is Bob's address; weird msgs will be forwarded to bob
  --- Bob listens on a UDP socket that the server sends to
  op appAddr:_ : Address -> Attribute  [ctor] .
  --- Alice's address will be in the query name
  --- op qRcd:_ : Config ->  Attribute [ctor] .  --- msgConf
       --- query msg pending response

  **** constructor for making a name server
  op mkNameServer : Address List{Record}  -> Actor [ctor] .  
  eq mkNameServer(ADDR, db) =
    < ADDR : Nameserver |
      db: db,
      queue: nilQueue,
      forwardonly: nullAddr,
      queriesFwd: nilTAQL >  .

  op isWeird : AttributeSet Msg -> Bool .
  ceq isWeird(wattrs, msg) = true
    if (to ADDR from  ADDR1 : query(ID, NAME, QTYPE)) := msg
    /\ nm(F) . NAME1 := NAME
    .
  eq isWeird(wattrs, msg) = false [owise] .

endm


mod PACKET_FRAGMENT_TEST is
  inc IODINE_NODE .

  op AliceAddr : -> Address . 

  op wClientAttrs : -> AttributeSet .
endm

---set trace on .
set print attribute on .
  
red nm(fragment(AliceAddr, 1, 1, 100, true)) . 'microsoft . 'com . root .
red fragment(nm(fragment(AliceAddr, 1, 1, 100, true))) ==  fragment(AliceAddr, 1, 1, 100, true) .
red fragment(label(1)) == nilFragment .

red makeFragments(packet(AliceAddr, 1, 100), 10) ==
  fragment(AliceAddr, 1, 1, 10, false) : fragment(AliceAddr, 1, 2, 10, false) : fragment(AliceAddr, 1, 3, 10, false) : fragment(AliceAddr, 1, 4, 10, false) : fragment(AliceAddr, 1, 5, 10, false) : fragment(AliceAddr, 1, 6, 10, false) : fragment(AliceAddr, 1, 7, 10, false)
    : fragment(AliceAddr, 1, 8, 10, false) : fragment(AliceAddr, 1, 9, 10, false) : fragment(AliceAddr, 1, 10, 10, true)
    .

red length(mtfl) == 0 .
red length(fragment(AliceAddr, 1, 1, 10, false) : fragment(AliceAddr, 1, 2, 10, false)) == 2 .

red handleNewPacket((wClientAttrs, seqCtr: 2), packet(AliceAddr, 1, 1000), 200) .
